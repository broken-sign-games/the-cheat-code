title The Cheat Code
author Menderbug
homepage menderbug.itch.io
text_color #b9bcb9

verbose_logging

run_rules_on_level_start

========
OBJECTS
========

Background
#227700 #7bc211
00000
00010
10000
00000
00100

Wall
#253200 #5e6400
01000
11111
00010
11111
01000

Water
#1850c7

ShoreUp
#68a6ff #ffffff
00000
.....
.....
.....
.....

ShoreDown
#68a6ff #ffffff
.....
.....
.....
.....
00000

ShoreLeft
#68a6ff #ffffff
0....
0....
0....
0....
0....

ShoreRight
#68a6ff #ffffff
....0
....0
....0
....0
....0

Splash1
#68a6ff #ffffff
.....
.....
..1..
.....
.....

Splash2
#68a6ff #ffffff
.....
..1..
.101.
..1..
.....

Splash3
#68a6ff #ffffff
.111.
10001
10.01
10001
.111.

Splash4
#68a6ff #ffffff
.000.
0...0
0...0
0...0
.000.

Border
black

Player
#983700 #000000 #fddbcf #4b30e3
.000.
.222.
22111
.333.
.3.3.

ExitingPlayer
#983700 #000000 #fddbcf #4b30e3
.000.
.222.
22111
.333.
.3.3.

DoorLocked
#b9bcb9 #000000 #ffffff
00120
01200
00120
01200
00120

DoorOpen
#b9bcb9 #000000 #ffffff
1....
....2
1....
....2
1....

DoorOpening
#b9bcb9 #000000 #ffffff
01..2
1..20
01..2
1..20
01..2

DoorClosing
#b9bcb9 #000000 #ffffff
01..2
1..20
01..2
1..20
01..2

DoorBackground
#4a4d4a

Crate
#b9bcb9 #000000 #ffffff
22220
20001
20101
20201
01111

ConveyorUp
#b9bcb9 #000000 #ffffff
00000
00200
02110
21111
00000

ConveyorDown
#b9bcb9 #000000 #ffffff
00000
21111
02110
00200
00000

ConveyorLeft
#b9bcb9 #000000 #ffffff
00020
00210
02110
00110
00010

ConveyorRight
#b9bcb9 #000000 #ffffff
02000
01200
01120
01100
01000

LeftInactive
#b9bcb9
.....
..0..
.00..
..0..
.....

LeftActive
#4b30e3
.....
..0..
.00..
..0..
.....

RightInactive
#b9bcb9
.....
..0..
..00.
..0..
.....

RightActive
#4b30e3
.....
..0..
..00.
..0..
.....

UpInactive
#b9bcb9
.....
..0..
.000.
.....
.....

UpActive
#4b30e3
.....
..0..
.000.
.....
.....

DownInactive
#b9bcb9
.....
.....
.000.
..0..
.....

DownActive
#4b30e3
.....
.....
.000.
..0..
.....

XInactive
#b9bcb9
.....
.0.0.
..0..
.0.0.
.....

XActive
#4b30e3
.....
.0.0.
..0..
.0.0.
.....

WhoopsMessage
transparent

_Next
transparent

_InvalidInput
pink

_DoorChanged
pink

=======
LEGEND
=======

. = Background
# = Wall
% = Border
~ = Water
P = Player
* = Crate
u = Border and UpInactive
d = Border and DownInactive
l = Border and LeftInactive
r = Border and RightInactive
x = Border and XInactive
& = Border and WhoopsMessage
o = DoorLocked
@ = DoorLocked and Player
4 = ConveyorLeft
6 = ConveyorRight
8 = ConveyorUp
2 = ConveyorDown

Movable = Player or Crate

InputInactive = UpInactive or DownInactive or LeftInactive or RightInactive or XInactive
InputActive = UpActive or DownActive or LeftActive or RightActive or XActive
Input = InputInactive or InputActive

Door = DoorLocked or DoorOpen or DoorOpening or DoorClosing

Conveyor = ConveyorUp or ConveyorDown or ConveyorLeft or ConveyorRight

Shore = ShoreUp or ShoreDown or ShoreLeft or ShoreRight
Splash = Splash1 or Splash2 or Splash3 or Splash4

=======
SOUNDS
=======

endlevel 76729903

Splash1 create 65311309 (30591509)

================
COLLISIONLAYERS
================

Background
DoorBackground
ExitingPlayer
Water, Conveyor, Door
ShoreUp
ShoreDown
ShoreLeft
ShoreRight
Splash
Player, Wall, Crate, Border
Input

WhoopsMessage
_Next

_DoorChanged
_InvalidInput

======
RULES
======

( Decorations )
[ Door ] -> [ Door DoorBackground ]
up [ Water no ShoreUp | no Water no Border ] -> [ Water ShoreUp | ]
down [ Water no ShoreDown | no Water no Border ] -> [ Water ShoreDown | ]
left [ Water no ShoreLeft | no Water no Border ] -> [ Water ShoreLeft | ]
right [ Water no ShoreRight | no Water no Border ] -> [ Water ShoreRight | ]

( Message in level 2 )
[ DoorLocked ] [ Player WhoopsMessage ] -> [ DoorLocked ] [ Player ] message Did I say "just"? Whoops.

((( Process Konami Code )))
[ moving Player ] -> [ moving Player _InvalidInput ]

( Advance code when given correct input )
right [ right Player _InvalidInput ] [ _Next RightInactive | ] -> [ right Player ] [ RightActive | _Next ]
right [ left Player _InvalidInput ] [ _Next LeftInactive | ] -> [ left Player ] [ LeftActive | _Next ]
right [ up Player _InvalidInput ] [ _Next UpInactive | ] -> [ up Player ] [ UpActive | _Next ]
right [ down Player _InvalidInput ] [ _Next DownInactive | ] -> [ down Player ] [ DownActive | _Next ]
right [ action Player _InvalidInput ] [ _Next XInactive | ] -> [ action Player ] [ XActive | _Next ]
( Also accept an Up without advancing when we're on the second active up )
right [ up Player _InvalidInput ] [ UpActive | _Next DownInactive ] -> [ up Player ] [ UpActive | _Next DownInactive ]

( Reset code otherwise )
[ _InvalidInput ] [ _Next ] -> [ _InvalidInput ] [ ]
[ _InvalidInput ] [ RightActive ] -> [ _InvalidInput ] [ RightInactive ]
[ _InvalidInput ] [ LeftActive ] -> [ _InvalidInput ] [ LeftInactive ]
[ _InvalidInput ] [ UpActive ] -> [ _InvalidInput ] [ UpInactive ]
[ _InvalidInput ] [ DownActive ] -> [ _InvalidInput ] [ DownInactive ]
[ _InvalidInput ] [ XActive ] -> [ _InvalidInput ] [ XInactive ]

right [ no Input | Input ] -> [ | Input _Next ]
right [ _Next | ... | _Next ] -> [ | ... | _Next ]

( Process Up once more since that's always the first required input )
right [ up Player _InvalidInput ] [ no Input | _Next UpInactive | ] -> [ up Player _InvalidInput ] [ | UpActive | _Next ]

[ _InvalidInput ] -> [ ]

( Process player trying to exit )
[ action Player DoorOpen ] -> [ ExitingPlayer DoorOpen ]

[ stationary Movable ConveyorUp ] -> [ up Movable ConveyorUp ]
[ stationary Movable ConveyorDown ] -> [ down Movable ConveyorDown ]
[ stationary Movable ConveyorLeft ] -> [ left Movable ConveyorLeft ]
[ stationary Movable ConveyorRight ] -> [ right Movable ConveyorRight ]

[ > Player | Crate ] -> [ > Player | > Crate ]
[ > Crate | Player ] -> [ > Crate | > Player ]

(----------- late rules -----------)

( Drop movables in water )
late [ Splash4 ] -> [ ]
late [ Splash3 ] -> [ Splash4 ] again
late [ Splash2 ] -> [ Splash3 ] again
late [ Splash1 ] -> [ Splash2 ] again
late [ Movable Water ] -> [ Water Splash1 ] again

( Conveyors )
late [ Movable Conveyor ] -> [ Movable Conveyor ] again

( Door )
late [ DoorOpen ] -> [ DoorClosing _DoorChanged ] again
( The door opens when we reach the X )
late [ DoorLocked ] [ XInactive _Next ] -> [ DoorOpening _DoorChanged ] [ XInactive _Next ] again
 
late [ DoorClosing no _DoorChanged ] -> [ DoorLocked _DoorChanged ]
late [ DoorOpening no _DoorChanged ] -> [ DoorOpen _DoorChanged ]
( This is for displaying the message in level 2 )
late [ DoorLocked _DoorChanged ] [ Player no WhoopsMessage ] [ WhoopsMessage ] -> [ DoorLocked _DoorChanged ] [ Player WhoopsMessage ] [ ] again

late [ _DoorChanged ] -> [ ]

==============
WINCONDITIONS
==============

some ExitingPlayer on DoorLocked

=======
LEVELS
=======

message Just enter the Konami Code to exit each level.

~~~~~~~~~~~
~~~~~~~~~~~
~~.......~~
~~.......~~
~~.......~~
~~...@...~~
~~.......~~
~~.......~~
~~.......~~
~~~~~~~~~~~
~~~~~~~~~~~
%uuddlrlrx%

~~~~~~~~~~~
~~~~~~~~~~~
~~.......~~
~~.####..~~
~~.#..##.~~
~~.#.@.#.~~
~~.##..#.~~
~~..####.~~
~~.......~~
~~~~~~~~~~~
~~~~~~~~~~~
%uuddlrlrx&

(

~~~~~~~~~~~
~~.......~~
~~.####..~~
~~.#..#..~~
~~.#.*##.~~
~~.#.po#.~~
~~.#*.*#.~~
~~.#..~~~~~
~~.###~~~~~
~~....~~~~~
~~~~~~~~~~~
%uuddlrlrx%

)

~~~~~~~~~~~
~.######.~~
~.#....#..~
~.#.~~.#..~
~.#.~**#..~
~.#.~..##.~
~.#..*po#.~
~.###...#.~
~...#..~~~~
~~..###~~~~
~~~~~~~~~~~
%uuddlrlrx%

~~~~~~~~~~~
~~~~~~~~~~~
~~.4.4.2.~~
~~8.6.2.8~~
~~.6.8.6.~~
~~8.4@4.8~~
~~.6.8.8.~~
~~6.6.8.4~~
~~.4.4.8.~~
~~~~~~~~~~~
~~~~~~~~~~~
%uuddlrlrx%

~~~~~~~~~~~
~~~~62~~~~~
~~..8..4.~~
~..*..o862~
~.6.82...2~
~.~.~688*4~
~..8...868~
~..*~~.~..~
~~....p..~~
~~~~~~~~~~~
~~~~~~~~~~~
%uuddlrlrx%

( probably not )
(
###########
##...#..o.#
#..o#..#.##
#.#..o.#o.#
#.........#
##o#op..#.#
#..##.#.o.#
#..#..o..##
##..o....o#
#o...#....#
#uuddlrlrx#
)