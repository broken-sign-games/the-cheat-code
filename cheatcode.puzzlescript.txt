title The Cheat Code
author Menderbug
homepage menderbug.itch.io

run_rules_on_level_start

========
OBJECTS
========

Background
#227700 #7bc211
00000
00010
10000
00000
00100

Wall
#253200 #5e6400
01000
11111
00010
11111
01000

Water
#1850c7

Border
black

Player
#983700 #000000 #fddbcf #4b30e3
.000.
.222.
22111
.333.
.3.3.

DoorLocked
#b9bcb9 #000000 #ffffff
00120
01200
00120
01200
00120

DoorOpen
green

Crate
#b9bcb9 #000000 #ffffff
22220
20001
20101
20201
01111

ConveyorUp
#b9bcb9 #000000 #ffffff
00000
00200
02110
21111
00000

ConveyorDown
#b9bcb9 #000000 #ffffff
00000
21111
02110
00200
00000

ConveyorLeft
#b9bcb9 #000000 #ffffff
00020
00210
02110
00110
00010

ConveyorRight
#b9bcb9 #000000 #ffffff
02000
01200
01120
01100
01000

LeftInactive
#b9bcb9
.....
..0..
.00..
..0..
.....

LeftActive
#4b30e3
.....
..0..
.00..
..0..
.....

RightInactive
#b9bcb9
.....
..0..
..00.
..0..
.....

RightActive
#4b30e3
.....
..0..
..00.
..0..
.....

UpInactive
#b9bcb9
.....
..0..
.000.
.....
.....

UpActive
#4b30e3
.....
..0..
.000.
.....
.....

DownInactive
#b9bcb9
.....
.....
.000.
..0..
.....

DownActive
#4b30e3
.....
.....
.000.
..0..
.....

XInactive
#b9bcb9
.....
.0.0.
..0..
.0.0.
.....

XActive
#4b30e3
.....
.0.0.
..0..
.0.0.
.....

WhoopsMessage
transparent

_Next
transparent

_InvalidInput
pink

=======
LEGEND
=======

. = Background
# = Wall
% = Border
~ = Water
P = Player
* = Crate
u = Border and UpInactive
d = Border and DownInactive
l = Border and LeftInactive
r = Border and RightInactive
x = Border and XInactive
& = Border and WhoopsMessage
o = DoorLocked
@ = DoorLocked and Player
4 = ConveyorLeft
6 = ConveyorRight
8 = ConveyorUp
2 = ConveyorDown

Movable = Player or Crate

InputInactive = UpInactive or DownInactive or LeftInactive or RightInactive or XInactive
InputActive = UpActive or DownActive or LeftActive or RightActive or XActive
Input = InputInactive or InputActive

Door = DoorLocked or DoorOpen

Conveyor = ConveyorUp or ConveyorDown or ConveyorLeft or ConveyorRight

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Water, Conveyor, Door
Player, Wall, Crate, Border
Input

WhoopsMessage
_Next

_InvalidInput

======
RULES
======

((( Process Konami Code )))
[ moving Player ] -> [ moving Player _InvalidInput ]

( Advance code when given correct input )
right [ right Player _InvalidInput ] [ _Next RightInactive | ] -> [ right Player ] [ RightActive | _Next ]
right [ left Player _InvalidInput ] [ _Next LeftInactive | ] -> [ left Player ] [ LeftActive | _Next ]
right [ up Player _InvalidInput ] [ _Next UpInactive | ] -> [ up Player ] [ UpActive | _Next ]
right [ down Player _InvalidInput ] [ _Next DownInactive | ] -> [ down Player ] [ DownActive | _Next ]
right [ action Player _InvalidInput ] [ _Next XInactive | ] -> [ action Player ] [ XActive | _Next ]

( Reset code otherwise )
[ _InvalidInput ] [ _Next ] -> [ _InvalidInput ] [ ]
[ _InvalidInput ] [ RightActive ] -> [ _InvalidInput ] [ RightInactive ]
[ _InvalidInput ] [ LeftActive ] -> [ _InvalidInput ] [ LeftInactive ]
[ _InvalidInput ] [ UpActive ] -> [ _InvalidInput ] [ UpInactive ]
[ _InvalidInput ] [ DownActive ] -> [ _InvalidInput ] [ DownInactive ]
[ _InvalidInput ] [ XActive ] -> [ _InvalidInput ] [ XInactive ]

right [ no Input | Input ] -> [ | Input _Next ]
right [ _Next | ... | _Next ] -> [ | ... | _Next ]

( Process Up once more since that's always the first required input )

(TODO: remain at the second ^ when up is pressed again)
right [ up Player _InvalidInput ] [ _Next UpInactive | ] -> [ up Player ] [ UpActive | _Next ]

[ _InvalidInput ] -> [ ]

( The door is open if we've reached or passed the X )
[ Door ] -> [ DoorOpen ]
[ Door ] [ XInactive no _Next ] -> [ DoorLocked ] [ XInactive ]

[ action Player DoorOpen ] -> [ Player DoorOpen ] win

[ action Player no DoorOpen ] [ DoorOpen ] [ WhoopsMessage ] -> [ Player ] [ DoorOpen ] [ ] message Did I say "just"? Whoops.

[ Movable ConveyorUp ] -> [ up Movable ConveyorUp ]
[ Movable ConveyorDown ] -> [ down Movable ConveyorDown ]
[ Movable ConveyorLeft ] -> [ left Movable ConveyorLeft ]
[ Movable ConveyorRight ] -> [ right Movable ConveyorRight ]

[ > Crate | Player ] -> [ > Crate | > Player ]
[ >  Player | Crate ] -> [ >  Player | > Crate ]

(----------- late rules -----------)

( Drop movables in water )
late [ Movable Water ] -> [ Water ]
late [ Movable Conveyor ] -> [ Movable Conveyor ] again

==============
WINCONDITIONS
==============

=======
LEVELS
=======

message Just enter the Konami Code to beat each level.

~~~~~~~~~~~
~~~~~~~~~~~
~~.......~~
~~.......~~
~~.......~~
~~...@...~~
~~.......~~
~~.......~~
~~.......~~
~~~~~~~~~~~
~~~~~~~~~~~
%uuddlrlrx%

~~~~~~~~~~~
~~~~~~~~~~~
~~.......~~
~~.####..~~
~~.#..##.~~
~~.#.@.#.~~
~~.##..#.~~
~~..####.~~
~~.......~~
~~~~~~~~~~~
~~~~~~~~~~~
%uuddlrlrx&

~~~~~~~~~~~
~~.......~~
~~.####..~~
~~.#..#..~~
~~.#.*##.~~
~~.#.po#.~~
~~.#*.*#.~~
~~.#..~~~~~
~~.###~~~~~
~~....~~~~~
~~~~~~~~~~~
%uuddlrlrx%

~~~~~~~~~~~
~.........~
~.........~
~.........~
~....~....~
~...6p....~
~...*o4...~
~..#.68...~
~.........~
~.........~
~~~~~~~~~~~
%uuddlrlrx%


( probably not )
(
###########
##...#..o.#
#..o#..#.##
#.#..o.#o.#
#.........#
##o#op..#.#
#..##.#.o.#
#..#..o..##
##..o....o#
#o...#....#
#uuddlrlrx#
)